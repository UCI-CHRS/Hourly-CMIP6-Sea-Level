
"""
This Snakefile includes all calculations, although Snakefiles 01-05 
were used instead to produce the final datasets. 
"""

import os
from pathlib import Path
import yaml
import pandas as pd

configfile: "config/config.yml"
configfile: "config/directories.yml"
configfile: "config/metadata.yml"

# Get parameter sets from config file
SRC_CONFIG = config['metadata_keys'][config['obs_source_params']]
GRD_CONFIG = config['metadata_keys'][config['grid_params']]
DIPMAC_CONFIG = config['metadata_keys'][config['dipmac_params']]
BASEDIR = config['BASE_DATA_PATH']
EMISSION_SCENARIOS = sorted({
    x for y in (
        config['comboparams']['cmipexperiments'][k]
        for k in config['comboparams']['cmipexperiments']
    )
    for x in y
})
BBOX = config['metadata_keys'][SRC_CONFIG['bbox']]
BBOX_CONUS = config['metadata_keys']['conus']


def makepath(*subpaths):
    """Convenience function. """
    return Path(config['BASE_DATA_PATH']).joinpath(*subpaths)


def all_input(wc):
    manifest = checkpoints.discover_cmip_latlon_space.get(**wc).output[0]
    df = pd.read_csv(manifest, sep="\t")
    return [
        f"{BASEDIR}/06_final/data/"
        f"{row.model}_{row.ripf}_{row.exp}_{m}_{a}_twl.nc"
        for _, row in df.iterrows()
        for m in config["comboparams"]["marginalfamilies"]
        for a in config["comboparams"]["acsfamilies"]
    ]


rule all: 
    input:
        all_input


## Preprocessing
include: "rules/run00_metadata.smk"
include: "rules/run01_rawdata.smk"
include: "rules/run02_processdata.smk"
include: "rules/run03_upsample.smk"

## Main computations
include: "rules/run04_parameterize.smk"

## Get valid CMIP models, lats, and lons
checkpoint discover_cmip_latlon_space:
    input:
        upsampled = f"{BASEDIR}/00_metadata/upsampled_data_manifest.csv",
        latlons = f"{BASEDIR}/00_metadata/param_available_latlons_manifest.csv"
    output:
        manifest = f"{BASEDIR}/00_metadata/valid_cmip_lat_lons_manifest.tsv"
    script:
        "scripts/buildwildcards.py"

# Trigger checkpoint evaluation
def cmip_space_input(_):
    # This is legal and forces the checkpoint to run
    return checkpoints.discover_cmip_latlon_space.get().output.manifest

rule collect_cmip_space:
    input:
        cmip_space_input
    output:
        touch(f"{BASEDIR}/00_metadata/cmip_space.done")


## Generate CMIP daily, monthly, and hourly TWL values. 
include: "rules/run05_coarsescale.smk"
include: "rules/run06_dipmac.smk"

# Produce compiled datasets
include: "rules/run07_compiletwl.smk"


## DAG
# snakemake -c1 all --rulegraph | dot -Tpng > dag.png
