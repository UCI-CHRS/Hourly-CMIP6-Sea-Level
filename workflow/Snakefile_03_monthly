
"""
Monthly sea level calculations, plus any daily calculations with a 
dependence on 03_upsampled data (can't be run on HPC)
"""

include: "rules/preliminaries.smk"


manifest_file = f"{BASEDIR}/00_metadata/valid_cmip_lat_lons_manifest.tsv"
df = pd.read_csv(manifest_file, sep="\t")
param_caches = [f"{BASEDIR}/00_metadata/param_available_latlons_manifest.csv"]
files = []
for r in df.itertuples():
    files.append(
        f"{BASEDIR}/05_cmip/{config['grid_params']}_"
        f"{config['obs_source_params']}_{config['dipmac_params']}/"
        f"{r.model}_{r.ripf}_{r.exp}/msl_monthly.nc"
    )
    files.append(
        f"{BASEDIR}/05_cmip/{config['grid_params']}_"
        f"{config['obs_source_params']}_{config['dipmac_params']}/"
        f"biascorr/{r.model}_{r.ripf}.nc"
    )
    files.append(
        f"{BASEDIR}/05_cmip/{config['grid_params']}_"
        f"{config['obs_source_params']}_{config['dipmac_params']}/"
        f"{r.model}_{r.ripf}_{r.exp}/"
        f"ntr_daily_lat_{r.lat}_lon_{r.lon}.csv"
    )


rule all:
    input:
        list(set(files))


# --------------------------------------------------------------------
# Get bias correction parameters (additive mean, multiplicative st dev shifts)
# --------------------------------------------------------------------


rule biascorrect:
    input:
        upsample = f"{BASEDIR}/00_metadata/touchfiles/upsample.done",
        cmip = (
            f"{BASEDIR}/03_upsampleddata/{config['grid_params']}/cmip/"
            "{model}_{ripf}_historical_daily.nc"
        )
    output:
        (
            f"{BASEDIR}/05_cmip/{config['grid_params']}_"
            f"{config['obs_source_params']}_{config['dipmac_params']}/"
            "biascorr/{model}_{ripf}.nc"
        )
    params:
        upsampled_atmos_daily = Path(
            f"{BASEDIR}/03_upsampleddata/{config['grid_params']}/"
            f"obs_atmos_{SRC_CONFIG['obs_atmos_source']}"
        ),
        glob_pattern_daily_atmos = f"{SRC_CONFIG['obs_atmos_source']}_daily_*.nc",
        sy = SRC_CONFIG['startyear'],
        ey = SRC_CONFIG['endyear'],
        ndays = GRD_CONFIG['ndays_window'],
        ngrids = GRD_CONFIG['ngrids_window'],

    script:
        "scripts/run05a_biascorrect.py"


rule calculatedatums:
    input:
        (
            f"{BASEDIR}/03_upsampleddata/{config['grid_params']}/cmip/"
            "{model}_{ripf}_historical_monthly.nc"
        )
    output:
        (
            f"{BASEDIR}/05_cmip/{config['grid_params']}_"
            f"{config['obs_source_params']}_{config['dipmac_params']}/"
            "{model}_{ripf}_{exp}/historical_monthly_datums.nc"
        )
    run:
        from pathlib import Path
        import xarray as xr
        Path(output[0]).parents[0].mkdir(exist_ok=True)
        Path(output[0]).parents[0].joinpath("hourly").mkdir(exist_ok=True)
        zos_zostoga = xr.open_dataset(input[0])
        zos_zostoga_datums = (
            zos_zostoga
            .sel(time=slice("1983-01-01", "2001-12-30"))
            .mean("time", skipna=True)
        )
        zos_zostoga_datums.to_netcdf(output[0])


rule monthlymsl:
    input:
        datums = (
            f"{BASEDIR}/05_cmip/{config['grid_params']}_"
            f"{config['obs_source_params']}_{config['dipmac_params']}/"
            "{model}_{ripf}_{exp}/historical_monthly_datums.nc"
        ),
        magicc = f"{BASEDIR}/04_params/magicc/{{exp}}.csv",
        cmip_monthly = (
            f"{BASEDIR}/03_upsampleddata/{config['grid_params']}/cmip/"
            "{model}_{ripf}_{exp}_monthly.nc"
        )
    output:
        f"{BASEDIR}/05_cmip/{config['grid_params']}_"
        f"{config['obs_source_params']}_{config['dipmac_params']}/"
        "{model}_{ripf}_{exp}/msl_monthly.nc"
    script:
        "scripts/run05b_monthlymsl.py"


rule dailyntr:
    input:
        bias_correction = (
            f"{BASEDIR}/05_cmip/{config['grid_params']}_"
            f"{config['obs_source_params']}_{config['dipmac_params']}/"
            "biascorr/{model}_{ripf}.nc"
        ),
        regression_daily = (
            f"{BASEDIR}/04_params/dipmac_regression/"
            f"{config['grid_params']}/{config['obs_source_params']}/"
            "lat_{lat}_lon_{lon}_ntr_daily_regression.csv"
        ),
        regression_monthly = (
            f"{BASEDIR}/04_params/dipmac_regression/"
            f"{config['grid_params']}/{config['obs_source_params']}/"
            "lat_{lat}_lon_{lon}_ntr_monthly_regression.csv"
        ),
        cmip_daily = (
            f"{BASEDIR}/03_upsampleddata/{config['grid_params']}/"
            "cmip/{model}_{ripf}_{exp}_daily.nc"
        ),
        cmip_sst = (
            f"{BASEDIR}/03_upsampleddata/{config['grid_params']}/"
            "cmip/{model}_{ripf}_{exp}_enso.nc"
        ),
        enso = f"{BASEDIR}/01_rawdata/obs/ocean/noaa/nino34.csv"
    output:
        (
            f"{BASEDIR}/05_cmip/{config['grid_params']}_"
            f"{config['obs_source_params']}_{config['dipmac_params']}/"
            "{model}_{ripf}_{exp}/"
            "ntr_daily_lat_{lat}_lon_{lon}.csv"
        )
    script:
        "scripts/run05c_dailyntr.py"
